data Result errTy successTy {
    Ok(successTy),
    Err(errTy)
}

data TimerId {
    TimerId(Number)
}

fun unTimerId(tid) {
    match tid {
        TimerId(t) => t
    }
}

let COMPILE_DELAY = 1000 // milliseconds

fun as_bool(a : a) : Boolean {
    let to_bool = _unsafe_js("function (x) { return !!x; }");
    to_bool(_unsafe_coerce(a))
}

fun querySelector(sel) {
    _unsafe_js("document").querySelector(sel)
}

fun getElementById(id) {
    _unsafe_js("document").getElementById(id)
}

fun setTimeout(f, delay) : TimerId {
    TimerId(_unsafe_js("window").setTimeout(f, delay))
}

fun clearTimeout(tid : TimerId) {
    _unsafe_js("window").clearTimeout(unTimerId(tid))
}

fun compileCrux(source) : Result String String {
    let rawRes = _unsafe_js("window").compileCrux(source)
    if as_bool(rawRes.error) {
        Err(rawRes.error)
    } else {
        Ok(rawRes.result)
    }
}

fun newXmlHttpRequest() : XHR {
    _unsafe_js("new XMLHttpRequest")
}

fun toJson(o) : String {
    _unsafe_js("JSON.stringify")(o)
}

fun parseJson(s) {
    _unsafe_js("JSON.parse")(s)
}

type XHR = {
    open: (String, String) -> (),
    setRequestHeader: (String, String) -> (),
    abort: () -> (),
    send: (String) -> (),
    onload: () -> (),
    onerror: (String) -> (),
    ontimeout: () -> (),
    response: String,
    mutable timeout: Number
}

type Compiler = {
            onresult:             (Result String String) -> (),
    mutable lastCompiledOptimize: Boolean,
    mutable xhr:                  Option XHR,
    mutable lastCompiledSource:   Option String,
    mutable timerId:              Option TimerId,
}

fun newCompiler(onresult) : Compiler {
    return {
        onresult: onresult,
        lastCompiledSource: None,
        lastCompiledOptimize: False,

        timerId: None,
        xhr: None
    }
}

fun compile(compiler : Compiler, source : String, optimize) {
    match compiler.timerId {
        Some(tid) => {
            clearTimeout(tid)
        }
        None => {}
    }

    match compiler.xhr {
        Some(x) => {
            x.abort()
            compiler.xhr = None
        }
        None => {}
    }

    match compiler.lastCompiledSource {
        Some(lcs) => {
            if lcs == source && optimize == compiler.lastCompiledOptimize {
                return ()
            }
        }
        _ => {
        }
    }

    compiler.timerId = Some(setTimeout(
        fun() {
            compiler.timerId = None

            let result = match compileCrux(source) {
                Err(error) => {
                    compiler.lastCompiledSource = Some(source)
                    compiler.onresult(Err("Compile error:\n" + error))
                    return ()
                }
                Ok(res) => {
                    if not(optimize) {
                        compiler.lastCompiledSource = Some(source)
                        compiler.onresult(Ok(res))
                        return ()
                    } else {
                        res
                    }
                }
            }

            let xhr = newXmlHttpRequest()
            compiler.xhr = Some(xhr)
            xhr.open("POST", "https://crux-closure-service.herokuapp.com/compile")
            xhr.setRequestHeader("content-type", "application/json")
            xhr.timeout = 15000 // 15s
            xhr.send(toJson({source: result}))

            xhr.onload = fun() {
                let result = parseJson(xhr.response)
                compiler.xhr = None
                compiler.lastCompiledSource = Some(source)
                compiler.onresult(Ok(result.source))
            }

            xhr.onerror = fun(e) {
                compiler.xhr = None
                compiler.lastCompiledSource = None
                compiler.onresult(Err("Network error:\n" + e))
            }

            xhr.ontimeout = fun() {
                compiler.xhr = None
                compiler.lastCompiledSource = None
                compiler.onresult(Err("Network timeout"))
            }
        }, COMPILE_DELAY
    ))
}

fun main() {
    let sourceTextArea = querySelector(".crux-playground .source")
    let outputTextArea = querySelector(".crux-playground .output")
    let optimizeCheckbox = querySelector(".crux-playground .optimize")
    let runButton = querySelector(".crux-playground .run")

    let loadExampleSource = fun() {
        let source = getElementById("initial_example").text
        // source = source.replace(/^\s+/, "") // trim leading whitespace
        sourceTextArea.value = source
    }

    let compiler = newCompiler(
        fun (result) {
            match result {
                Ok(res) => {
                    outputTextArea.classList.remove("has-errors")
                    outputTextArea.value = res
                }
                Err(err) => {
                    outputTextArea.classList.add("has-errors")
                    outputTextArea.value = err
                }
            }
        }
    )

    let recompile = fun() {
        let content = sourceTextArea.value
        let optimize = optimizeCheckbox.checked
        compile(compiler, content, optimize)
    }

    let registerCompileListener = fun() {
        sourceTextArea.addEventListener("input", recompile)
        optimizeCheckbox.addEventListener("change", recompile)
    }

    loadExampleSource()
    registerCompileListener()
    recompile()

    sourceTextArea.disabled = False
    sourceTextArea.setSelectionRange(0, 0)
    sourceTextArea.focus()

    runButton.addEventListener("click", fun() {
        print("run button temporarily disabled")
    })
}

let _ = main()
