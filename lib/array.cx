pragma { NoBuiltin }

export data Array a {}

let _unsafe_new: (Number) -> [a] =
    _unsafe_js("function (len) { return new Array(len); }")

let _unsafe_set = _unsafe_js("function (arr, idx, el) { arr[idx] = el; }")
let _unsafe_get = _unsafe_js("function (arr, idx) { return arr[idx]; }")

export forall {t} fun get(a: [t], idx: Number) {
    _unsafe_get(a, idx)
}

export forall {a} fun len(a: [a]): Number {
    _unsafe_coerce(a).length
}

export forall {a} fun replicate(element: a, length: Number): [a] {
    let arr = _unsafe_new(length);

    let mutable i = 0;
    while i < length {
        _unsafe_set(arr, i, element)
        i = i + 1
    };

    arr
}

export forall {a, b} fun each(arr: [a], f: (a) -> b): () {
    let mutable i = 0
    let length = len(arr)
    while i < length {
        f(_unsafe_get(arr, i))
        i = i + 1
    }
}

export forall {a} fun sliceFrom(arr: [a], start: Number): [a] {
    _unsafe_coerce(arr).slice(start)
}

export forall {a} fun sliceTo(arr: [a], end: Number): [a] {
    _unsafe_coerce(arr).slice(0, end)
}

export forall {a} fun slice(arr: [a], start: Number, end: Number): [a] {
    _unsafe_coerce(arr).slice(start, end)
}
