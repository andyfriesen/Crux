pragma { NoBuiltin }

import {
    array
    string
    number
}

// TODO: implement selective import
type Number = number.Number

export data MutableArray a {}

let _unsafe_new: fun(Number) -> mutable [a] =
    _unsafe_js("function (len) { return new Array(len); }")

let _unsafe_set = _unsafe_js("function (arr, idx, el) { arr[idx] = el; }")
let _unsafe_get = _unsafe_js("function (arr, idx) { return arr[idx]; }")

export fun append{a}(a: mutable [a], v: a): () {
    _unsafe_coerce(a).push(v)
}

export fun get{a}(a: mutable [a], idx: Number) {
    _unsafe_get(a, idx)
}

export fun len{a}(a: mutable [a]): Number {
    _unsafe_coerce(a).length
}

export fun replicate{a}(element: a, length: Number): mutable [a] {
    let arr = _unsafe_new(length);

    let mutable i = 0;
    while i < length {
        _unsafe_set(arr, i, element)
        i = i + 1
    };

    arr
}

export fun each{a, b}(arr: mutable [a], f: fun(a) -> b): () {
    let mutable i = 0
    let length = len(arr)
    while i < length {
        f(_unsafe_get(arr, i))
        i = i + 1
    }
}

export fun sliceFrom{a}(arr: mutable [a], start: Number): mutable [a] {
    _unsafe_coerce(arr).slice(start)
}

export fun sliceTo{a}(arr: mutable [a], end: Number): mutable [a] {
    _unsafe_coerce(arr).slice(0, end)
}

export fun slice{a}(arr: mutable [a], start: Number, end: Number): mutable [a] {
    _unsafe_coerce(arr).slice(start, end)
}

export fun freeze{a}(arr: mutable [a]): [a] {
    _unsafe_coerce(arr).slice()
}

// TODO: add comparator support
export fun sort(arr: mutable [string.String]) {
    _unsafe_coerce(arr).sort()
}
