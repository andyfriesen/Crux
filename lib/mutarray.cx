pragma { NoBuiltin }

import { array }

export data MutableArray a {}

let _unsafe_new: (Number) -> mutable [a] =
    _unsafe_js("function (len) { return new Array(len); }")

let _unsafe_set = _unsafe_js("function (arr, idx, el) { arr[idx] = el; }")
let _unsafe_get = _unsafe_js("function (arr, idx) { return arr[idx]; }")

export fun append(a: mutable [a], v: a): () {
    _unsafe_coerce(a).push(v)
}

export fun get(a: mutable [a], idx: number) {
    _unsafe_get(a, idx)
}

export fun len(a: mutable [a]): Number {
    _unsafe_coerce(a).length
}

export fun replicate(element: a, length: Number): mutable [a] {
    let arr = _unsafe_new(length);

    let mutable i = 0;
    while i < length {
        _unsafe_set(arr, i, element)
        i = i + 1
    };

    arr
}

export fun each(arr: mutable [a], f: (a) -> b): () {
    let mutable i = 0
    let length = len(arr)
    while i < length {
        f(_unsafe_get(arr, i))
        i = i + 1
    }
}

export fun sliceFrom(arr: mutable [a], start: Number): mutable [a] {
    _unsafe_coerce(arr).slice(start)
}

export fun sliceTo(arr: mutable [a], end: Number): mutable [a] {
    _unsafe_coerce(arr).slice(0, end)
}

export fun slice(arr: mutable [a], start: Number, end: Number): mutable [a] {
    _unsafe_coerce(arr).slice(start, end)
}

export fun freeze(arr: mutable [a]): [a] {
    _unsafe_coerce(arr).slice()
}

// TODO: add comparator support
export fun sort(arr: mutable [String]) {
    _unsafe_coerce(arr).sort()
}
